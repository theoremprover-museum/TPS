% Blackboard bold ligatures, Alan Jeffrey, 13--18 Dec 1989.
%
% Provides --, ---, `` and ''.
% These are put in positions
%
%   -- oct "173"
%  --- oct "175"
%   `` oct "176"
%   '' oct "042"

ligtable       "-": "-" =: oct "173";
ligtable oct "173": "-" =: oct "175";
ligtable       "`": "`" =: oct "176";
ligtable       "'": "'" =: oct "042";

% -------------------------------------------------------------------

bbchar
   (oct "173")
   (smallgap#, 0.5bigcap#, smallgap#)
   (aTop#, aBottom#);

   "En dash `--'";

   % The line.
   draw
      dashMiddle~Left
         -- dashMiddle~Right

endchar;

% -------------------------------------------------------------------
 
bbchar 
   (oct "175")
   (smallgap#, bigcap#, smallgap#)
   (aTop#, aBottom#);

   "Em dash `---'";

   % The line.
   draw
      dashMiddle~Left
         -- dashMiddle~Right

endchar;

% -------------------------------------------------------------------

bbchar 
   (oct "176")
   (medgap#, 2tiny# + space#, medgap#)
   (bTop#, bTop# - tiny# - comma_depth#);

   "Opening quotes ```'";

   % The left quote.
   draw
      splodgel above rightof Bottom~Left
         .. farrightof Top~Left;

   % The right quote.
   draw
      splodgel above leftof Bottom~Right
         .. Top~Right

endchar;

% -------------------------------------------------------------------

bbchar 
   (oct "042")
   (medgap#, 2tiny# + space#, medgap#)
   (bTop#, bTop# - tiny# - comma_depth#);

   "Closing quotes `'''";

   % The left quote
   draw
      splodger below rightof Top~Left
         .. Bottom~Left;

   % The right quote
   draw
      splodger below leftof Top~Right
         .. farleftof Bottom~Right

endchar;
  
% -------------------------------------------------------------------

